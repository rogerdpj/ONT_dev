/*
DSL2 channels
*/
nextflow.enable.dsl=2

log.info """\

WGS - P A R A M E T R E S
==============================================
Configuration environemnt:
    Out directory:             $params.outdir
    Fastq directory:           $params.input
    Reference directory:       $params.reference
"""
    .stripIndent()

//Call all the sub-work

include { QC                                            }     from '../bin/qc/main'
include { TRIMMING                                      }     from '../bin/trimming/main'
include { NANOCOMP                                      }     from '../bin/qc/nanocomp/main'
include { SUB_SAMPLE_2 as ASSEMBLE                      }     from '../bin/assemble/fly/main'
include { POLISHING_ROUND                               }     from '../bin/polishing/main'
include { MEDAKA                                        }     from '../bin/assemble/medaka/main'
include { WRAP                                          }     from '../bin/polishing/wrap_2'
include { PROKKA                                        }     from '../bin/annotation/prokka/main'
include { BAKTA                                         }     from '../bin/annotation/bakta/main_2'
include { AGT                                           }     from '../bin/annotation/main_2'
include { BUSCO                                         }     from '../bin/qc/busco/main'
include { QUAST                                         }     from '../bin/qc/quast/main'
include { MULTIQC                                       }     from '../bin/qc/multiqc/main'
include { AMR                                           }     from '../bin/AMR/abricate/main'
include { AMR_2                                         }     from '../bin/AMR/resfinder/main'
include { MLST                                          }     from '../bin/mlst/main'

workflow assemble {
    preprocess_output = pre_process()
    assambleprocess_output = assamble_process(preprocess_output.trimming_ch, preprocess_output.trimming_ch_2, preprocess_output.pre_data_qc)
    amrprocess_output = amr_process (assambleprocess_output.consensum_file_ch)
}

workflow pre_process {
    take:
    main:
    barcode_dir_ch = channel.fromPath(params.input, type: 'dir').map{barcode_dir -> tuple(barcode_dir.baseName, barcode_dir)}
    qc_ch = QC(barcode_dir_ch)
    trimming_before_ch = TRIMMING(qc_ch.fastq_combine)
    trimming_ch = trimming_before_ch.barcodefile_gz
    trimming_ch_2 = trimming_before_ch.barcodefile_compress
    pre_data_qc = qc_ch.fastq_combine

    emit:
    trimming_ch
    trimming_ch_2
    pre_data_qc

}

workflow assamble_process {
    take:
    trimming_ch
    trimming_ch_2
    pre_data_qc
    
    main:

    nocomp_ch = NANOCOMP(pre_data_qc.map{i -> i[1]}.collect(), trimming_ch_2.map{i -> i[1]}.collect())

    genome_size_ch = Channel
                        .fromPath(params.genome_size_file)
                        .splitCsv(header: true)
                        .map { row -> tuple(row.barcode, row.genome_size as int, row.sample_code) }

    reads_with_size_ch = trimming_ch.join(genome_size_ch)

    fly_ch = ASSEMBLE(reads_with_size_ch)

//POLISHING PROCESS
//Porcesar el emit del assembly en fly para determinar numeros de polishing
coverage_ch = fly_ch.info_cov
    .map { sample_code, info_file -> 
        // Lee el archivo `assembly_info.txt` y extrae la cobertura
        def cov_value = info_file
            .text
            .split("\n")  // Divide en líneas
            .drop(1)      // Omite la cabecera
            .collect { line -> line.split("\t")[2] as int }[0]  // Extrae la columna 'cov.' (índice 2) y convierte a int
        tuple(sample_code, cov_value)
    }


// Creacion de channel que combina los input para el procesamiento de polishing en relacion al coverage obtneido en fly 
    sample_fixe = reads_with_size_ch.map { tupla ->
        def pathread = tupla [1]
        def sample_code = tupla [3]
        return tuple(sample_code, pathread)}

    polished_ch = sample_fixe
        .join(fly_ch.fly_assambly_tuple)
        .join(coverage_ch)
        .map { sample_code, trimmed_reads, assembly_fasta, cov_value -> 
            def max_rounds = (cov_value <= 14) ? 8 : 5 //asignacion de numero de polishing ( nªround each raund inclue: minimap + racon )
            tuple(sample_code, trimmed_reads, assembly_fasta, max_rounds)
        }

    polished_ch_final = POLISHING_ROUND(polished_ch)

    medaka_ch = sample_fixe
    .join(polished_ch_final)
    .map { sample_code, trimmed_reads, final_polishing_fasta ->
        tuple(sample_code, trimmed_reads, final_polishing_fasta)
    }
   
    medaka_consensum_ch= MEDAKA(medaka_ch)

    consensum_file_ch = medaka_consensum_ch.assemble_medaka

    wrap_ch = WRAP(medaka_consensum_ch.assemble_medaka)

    prokka_ch = PROKKA (wrap_ch)

    bakta_ch = BAKTA (wrap_ch)

    AGT(prokka_ch.prokka_gff, bakta_ch.bakta_gff3, wrap_ch)

    busco_ch = BUSCO(medaka_consensum_ch.assemble_medaka)
    
    quast_ch = QUAST(medaka_consensum_ch.assemble_medaka)

    //MULTIQC Directory for analysis

    multiqc_ch = MULTIQC( busco_ch.map{i -> i[1]}.collect(), quast_ch.map{i -> i[1]}.collect())

    emit:
    consensum_file_ch

}

workflow amr_process {

    take:

    consensum_file_ch

    main:
    
    amr_ch = AMR(consensum_file_ch, params.organism)
    amr_2_ch = AMR_2(consensum_file_ch)
    mlst_ch = MLST(consensum_file_ch)
    
}